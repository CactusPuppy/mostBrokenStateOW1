import "main.ostw";

disabled rule: '=========== GENERAL ===========' {}

rule: 'Track baseHero'
Event.OngoingPlayer
if (baseHero != HeroOf())
{
  cleanupHero();
  baseHero = HeroOf();
  effectiveHero = HeroOf();
  MinWait();
  LoopIfConditionIsTrue();
}

void cleanupHero() playervar '[general.ostw] Clean up previous hero stuff'
{
  AbortIf(baseHero == null);
  RemoveAllHealthPoolsFromPlayer(EventPlayer());
  StopHealingModification(selfHealMod);
  SetMaxHealth(EventPlayer(), 100);
}

void setupHero() playervar '[general.ostw] Set up new hero stuff'
{
  switch (baseHero) {
    case Hero.Dva:
      setupDVa();
      break;
    case Hero.Orisa:
      setupOrisa();
      break;
    case Hero.Reinhardt:
      setupReinhardt();
      break;
    case Hero.Baptiste:
      setupBaptiste();
      break;
    case Hero.Brigitte:
      setupBrigitte();
      break;
    case Hero.Mercy:
      setupMercy();
      break;
  }
}

rule: '[general.ostw] Track effectiveHero'
Event.OngoingPlayer
Player.Echo
if (IsDuplicating())
{
  effectiveHero = HeroBeingDuplicated();
  WaitUntil(!IsDuplicating(), 9999);
  effectiveHero = HeroOf();
  MinWait();
  LoopIfConditionIsTrue();
}
